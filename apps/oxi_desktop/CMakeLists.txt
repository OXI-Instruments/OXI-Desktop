if(WIN32)
    # See about TARGET_RUNTIME_DLLS from 3.21 https://cmake.org/cmake/help/git-stage/manual/cmake-generator-expressions.7.html#genex:TARGET_RUNTIME_DLLS
    cmake_minimum_required(VERSION 3.21)
else()
    cmake_minimum_required(VERSION 3.5)
endif()

project(oxi_desktop VERSION 0.1 LANGUAGES CXX)

set(CMAKE_INCLUDE_CURRENT_DIR ON)

set(CMAKE_AUTOUIC ON)
set(CMAKE_AUTOMOC ON)
set(CMAKE_AUTORCC ON)

set(CMAKE_CXX_STANDARD 17)
set(CMAKE_CXX_STANDARD_REQUIRED ON)

find_package(QT NAMES Qt6 Qt5 REQUIRED COMPONENTS Core Widgets)
find_package(Qt${QT_VERSION_MAJOR} REQUIRED COMPONENTS Core Widgets)

set(PROJECT_SOURCES
        main.cpp
        mainwindow.cpp
        mainwindow.h
        mainwindow.ui
        oxi_desktop.qrc
)

if(${QT_VERSION_MAJOR} GREATER_EQUAL 6)
    if (WIN32)
        set(app_icon_resource_windows "${CMAKE_CURRENT_SOURCE_DIR}/resources/oxi_desktop.rc")
        qt_add_executable(oxi_desktop
            MANUAL_FINALIZATION
            ${PROJECT_SOURCES}
            ${app_icon_resource_windows})
    elseif (APPLE)
        # The MACOSX_BUNDLE_ICON_FILE variable is added to the Info.plist
        # generated by CMake. This variable contains the .icns file name,
        # without the path.
        set(MACOSX_BUNDLE_ICON_FILE icon.icns)

        # And the following tells CMake where to find and install the file itself.
        set(app_icon_macos "${CMAKE_CURRENT_SOURCE_DIR}/resources/icon.icns")
        set_source_files_properties(${app_icon_macos} PROPERTIES
               MACOSX_PACKAGE_LOCATION "Resources")

        qt_add_executable(oxi_desktop MACOSX_BUNDLE
            MANUAL_FINALIZATION
            ${PROJECT_SOURCES}
            ${app_icon_macos})
    else()
        qt_add_executable(oxi_desktop
            MANUAL_FINALIZATION
            ${PROJECT_SOURCES})
    endif()
else()
    if (WIN32)
        set(app_icon_resource_windows "${CMAKE_CURRENT_SOURCE_DIR}/resources/oxi_desktop.rc")
        add_executable(oxi_desktop
            ${PROJECT_SOURCES}
            ${CMAKE_SOURCE_DIR}/libs
            ${app_icon_resource_windows})
    elseif(APPLE)
        set(MACOSX_BUNDLE_ICON_FILE icon.icns)
        set(app_icon_macos "${CMAKE_CURRENT_SOURCE_DIR}/resources/icon.icns")
        set_source_files_properties(${app_icon_macos} PROPERTIES
               MACOSX_PACKAGE_LOCATION "Resources")

        add_executable(oxi_desktop MACOSX_BUNDLE
            ${PROJECT_SOURCES}
            ${CMAKE_SOURCE_DIR}/libs
            ${app_icon_macos})
    else()
        add_executable(oxi_desktop
            ${PROJECT_SOURCES}
            ${CMAKE_SOURCE_DIR}/libs
        )
    endif()
endif()

target_link_libraries(oxi_desktop
    PRIVATE Qt${QT_VERSION_MAJOR}::Core
            Qt${QT_VERSION_MAJOR}::Widgets
            QMidi
            oxi_core)

set_target_properties(oxi_desktop PROPERTIES
    MACOSX_BUNDLE_GUI_IDENTIFIER my.example.com
    MACOSX_BUNDLE_BUNDLE_VERSION ${PROJECT_VERSION}
    MACOSX_BUNDLE_SHORT_VERSION_STRING ${PROJECT_VERSION_MAJOR}.${PROJECT_VERSION_MINOR}
    MACOSX_BUNDLE TRUE
    WIN32_EXECUTABLE TRUE
)

set_target_properties(oxi_desktop
    PROPERTIES OUTPUT_NAME "OXI-Desktop")

string(TOUPPER ${CMAKE_BUILD_TYPE} OXI_BUILD_TYPE_UPPER)
if(OXI_BUILD_TYPE_UPPER STREQUAL "RELEASE" AND WIN32)
    message("RELEASE build on windows will copy runtime assemblies to target location.")
    add_custom_command(TARGET oxi_desktop POST_BUILD
      COMMAND ${CMAKE_COMMAND} -E copy $<TARGET_RUNTIME_DLLS:oxi_desktop> $<TARGET_FILE_DIR:oxi_desktop>
      COMMAND_EXPAND_LISTS
    )
endif()

if(QT_VERSION_MAJOR EQUAL 6)
    qt6_finalize_target(oxi_desktop)
endif()
